/*
* Vulkan Example - Attraction based compute shader particle system
*
* Updated compute shader by Lukas Bergdoll (https://github.com/Voultapher)
*
* Copyright (C) 2016 by Sascha Willems - www.saschawillems.de
*
* This code is licensed under the MIT license (MIT) (http://opensource.org/licenses/MIT)
*/

#version 450

struct Particle
{
	vec4 position;
	vec4 velocity;
};

// Binding 0 : Position storage buffer
layout(std140, binding = 0) buffer Vertex 
{
    Particle particles[ ];
} inVertex;

layout (binding = 1) uniform ParticleParam 
{
	vec4 data0;
	vec4 data1;
} param;

layout (local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

vec2 Repulsion(vec2 pos, vec2 dst)
{
	vec2 delta = dst - pos;
	float dist = sqrt(dot(delta, delta));
	return delta * (1.0 / (dist * dist * dist)) * -0.000035;
}

vec2 Attraction(vec2 pos, vec2 dst) 
{
    vec2 delta = dst - pos;
	const float damp = 0.5;
    float dampedDot  = dot(delta, delta) + damp;
    float invDist    = 1.0 / sqrt(dampedDot);
    float invDist3   = invDist * invDist * invDist;
    return delta * invDist3 * 0.0035;
}

void main() 
{
    uint globalIndex = gl_WorkGroupID.z * gl_NumWorkGroups.x * gl_NumWorkGroups.y + gl_WorkGroupID.y * gl_NumWorkGroups.x + gl_WorkGroupID.x;
	globalIndex = globalIndex * gl_WorkGroupSize.x * gl_WorkGroupSize.y * gl_WorkGroupSize.z;
	globalIndex = globalIndex + gl_LocalInvocationIndex;

	if (globalIndex >= param.data0.w) {
		return;
	}

	vec2 vPos = inVertex.particles[globalIndex].position.xy;
	vec2 vVel = inVertex.particles[globalIndex].velocity.xy;
	vec2 vDst = param.data0.xy;

	vVel += Repulsion(vPos, vDst) * param.data1.z;
	vPos += vVel * param.data0.z;

	if (vPos.x < -1.0 || vPos.x > 1.0 || vPos.y < -1.0 || vPos.y > 1.0) {
		vVel = (-vVel * 0.1) + Attraction(vPos, vDst) * param.data1.w;
	}
	else {
		inVertex.particles[globalIndex].position.xy = vPos;
	}

	inVertex.particles[globalIndex].position.z += 0.01 * param.data0.z;
	if (inVertex.particles[globalIndex].position.z > 1.0) {
		inVertex.particles[globalIndex].position.z = 0.0;
	}

	inVertex.particles[globalIndex].velocity.xy = vVel;
}